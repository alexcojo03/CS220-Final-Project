class Snake {
    field int direction;
    field List segments;

    constructor Snake new(Pos startPos, int startDir) {
        let direction = startDir;
        let segments = List.new(startPos);

        return this;
    }

    // Returns lengths of snake
    method int getLength() {
        return segments.getSize();
    }

    // direction of snake
    method int getDirection() {
        return direction;
    }

    method void setDirection(int newDirection) {
        let direction = newDirection;
        return;
    }

    // Only draws head of snake using first position in list
    method void drawHead() {
        var int xPos, yPos;
        var Pos headPos;
        let headPos = segments.getFirstData();
        let xPos = headPos.getX();
        let yPos = headPos.getY();
        
        // Make sure to draw
        do Screen.setColor(1);
        do Screen.drawRectangle(xPos, yPos, xPos + 15, yPos + 15);
        
        return;
    }

    // Erases the tail (last position in segment)
    method void eraseTail() {
        var int xPos, yPos;
        var Pos tail;
        let tail = segments.popBack();
        let xPos = tail.getX();
        let yPos = tail.getY();

        // Draw in white to erase
        do Screen.setColor(0);
        do Screen.drawRectangle(xPos, yPos, xPos + 15, yPos + 15);
        // Garbage collection
        do tail.dispose();
        
        return;
    }

    // Updates only head coordinates based on where snake is moving
    method void updateHead() {
        var int nextX, nextY;
        var Pos head;
        // Get first item in list of segment (the head)
        let head = segments.getFirstData();

        let nextX = head.getX();
        let nextY = head.getY();
        // update head coordinates based on snakes direction
        /*  0 - Up, 1 - Right, 2 - Down, 3 - Left */
        if (direction = 0) { 
            let nextY = head.getY() - 16;
        } 
        if (direction = 1) { 
            let nextX = head.getX() + 16;
        } 
        if (direction = 2) { 
            let nextY = head.getY() + 16;
        } 
        if (direction = 3) { 
            let nextX = head.getX() - 16;
        } 
        let head = Pos.new(nextX, nextY);
        
        // Add the new head to list of segments
        do segments.pushFront(head);
        

        return;
    }

    // Grow snake by only updating it head and not its tail
    method void grow() {
        do updateHead();   
        do drawHead();
        
        return;
    }

    // Move snake by growing its head and then its tail
    method void move() {
        // Adds a new head to the list
        do updateHead();   
        // Erase tail first so that head is not erased 
        // if they happen to be at same position
        do eraseTail();
        // draws the added head
        do drawHead();

        return;
    }

    // Checks if the snake hits itself
    method boolean hitsItself() {
        var Pos head;
        let head = segments.getFirstData();
        // Check if head is in same position as any other part of the body
        // when searching the body, don't include head, so start search from index 1
        return segments.contains(1, head);
    }

    // checks if a position is part of the snake
    method boolean containsSegment(Pos segment) {
        return segments.contains(0, segment);
    }

    // Check if snake is out of bounds
    method boolean isOutOfBounds() {
        var int headX, headY;
        var Pos head;

        let head = segments.getFirstData();
        let headX = head.getX();
        let headY = head.getY();
        
        // Determine where the next position of the head
        // will be based on snakes direction
        if (direction = 0) {
            let headY = headY - 16;
        }
        if (direction = 1) {
            let headX = headX + 16;
        }
        if (direction = 2) {
            let headY = headY + 16;
        }
        if (direction = 3) {
            let headX = headX - 16;
        }

        // check left side
        // Since x, y is top left corner for each pos object
        // compensate by adding width of each square
        if (headX < 0) {
            return true;
        }
        // check top side
        if (headY < 0) {
            return true;
        }
        // check right side
        if (headX > 497) {
            return true;
        }
        // check bottom side
        if (headY > 241) {
            return true;
        }
        return false;
    }

    // Returns position of the head of the snake
    method Pos getHeadPosition() {
        return segments.getFirstData();
    }

    method void dispose() {
        do segments.dispose();
        do Memory.deAlloc(this);
        return;
    }

}