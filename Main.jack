class Main {
    static SnakeGame game;

    function void main() {
        var int choice, score, seed;

        let choice = 82;
        let score = 0;

        while(choice = 82) {
            let choice = Main.printTitle();
            
            // Generate new game with random seed of the users input key + their score
            // from the previous game
            let seed = score + choice;
            let game = SnakeGame.new(32, 16, 1, seed);
            // Begin and end game here
            do Main.startGame();
            let score = game.getScore();
            do Main.endGame();
            let choice = Main.waitForKey();
            // only allow q or r to be pressed 
            while(~((choice = 82) | (choice = 81))) {
                let choice = Main.waitForKey();
            }
        }
        return;
    }
    function void startGame() {
        do Screen.clearScreen();
        do game.generateNewFruit();
        do game.run();
        return;
    }

    // Clears the game and prints ending screen
    function void endGame() {
        // Characters are 7 pixels wide, 10 pixels high
        do Output.moveCursor(13, 26);
        do Output.printString("Game Over");
        do Output.moveCursor(14, 12);
        do Output.printString("Press 'Q' to quit or 'R' to restart");
        do game.dispose();

        return;
    }


    // Prints title of game and waits for key to be pressed
    function int printTitle() {
        var int key;
        do Screen.clearScreen();
        do Output.moveCursor(10,20);
        do Output.printString("Welcome to Snake");
        do Output.moveCursor(12,16);
        do Output.printString("Press any key to begin");
        // Wait for any key and return that input
        let key = Main.waitForKey();
        do Screen.clearScreen();
        return key;
    }

    // Waits for user input
    function char waitForKey() {
        var char key;
        let key = 0;
        while(key = 0) {
            let key = Keyboard.keyPressed();
        }
        
        return key;
    }
}